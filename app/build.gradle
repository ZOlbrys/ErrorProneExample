apply plugin: 'com.android.application'
apply plugin: 'net.ltgt.errorprone'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        // See https://github.com/tbroyer/gradle-errorprone-plugin/releases for version information
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.6.1'
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.errorproneexample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        allErrorsAsWarnings = true
        check('NullAway', net.ltgt.gradle.errorprone.CheckSeverity.WARN)
        option('NullAway:AnnotatedPackages', 'com.example')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'

    errorprone 'com.google.errorprone:error_prone_core:2.3.2'
    errorproneJavac 'com.google.errorprone:javac:9-dev-r4023-3'
    errorprone 'com.uber.nullaway:nullaway:0.6.4'

    // Mockito
    def mockitoVersion = '2.23.4'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
}
